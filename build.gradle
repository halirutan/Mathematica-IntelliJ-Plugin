buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "org.jetbrains.intellij" version "0.3.1"
}

apply plugin: 'java'
apply plugin: "kotlin"

//noinspection GroovyAssignabilityCheck,GrUnresolvedAccess
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    //noinspection GrUnresolvedAccess
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// On travis, we don't want to deploy to Jetbrains that requires credentials
if (project.file('gradle.properties').exists()) {
    apply from: "gradle.properties"
}

// run this task for profiling the running sandbox
task runIdeProfiler(type: org.jetbrains.intellij.tasks.RunIdeTask) {
    jvmArgs = ["-agentpath:/home/patrick/build/share/yjp-2017.02/bin/linux-x86-64/libyjpagent.so=delay=2000"]
}

sourceSets {
    //noinspection GroovyAssignabilityCheck
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['tests']
        resources.srcDirs = ['testData']
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile fileTree(dir: 'lib', include: ['*.jar'])
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

// Information  about the plugin

// Plugin version number
version '3.0beta2'

intellij {
    version = '2018.1.3'
    downloadSources = true
    pluginName = 'Mathematica-IntelliJ-Plugin'
    sameSinceUntilBuild = true
    updateSinceUntilBuild = true
}

// Simple function to load change-notes.html and description.html into valid text for plugin.xml
def htmlFixer = {htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '')}

patchPluginXml {
    changeNotes = htmlFixer('resources/META-INF/change-notes.html')
    pluginDescription = htmlFixer('resources/META-INF/description.html')
    sinceBuild = '181'
}

publishPlugin {
    channels 'beta'
}